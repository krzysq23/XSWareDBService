plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.25'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.25'
}

group = 'pl.xsware'
version = '0.1-sr1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

bootJar {
	exclude("application-dev.yml", "application-prod.yml", "log4j2.xml")
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	launchScript()
}

jar {
	enabled = false
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	all*.exclude module : 'spring-boot-starter-logging'
}

repositories {
	mavenCentral()
}

dependencies {

	// spring boot
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	// Jakarta
	implementation 'org.glassfish:jakarta.el:4.0.2'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

	// Hibernate
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

	// log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'

	// Database MySQL Connector
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Swagger
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13"

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
	}
}

tasks.register('copyAppYml', Copy) {
	from './src/main/resources/application-prod.yml'
	from './src/main/resources/log4j2.xml'
	into "./build/libs/config"
	rename "-prod", ""
}

tasks.named('build') {
	dependsOn tasks.named('copyAppYml')
}

allOpen {
	annotation 'jakarta.persistence.Entity'
	annotation 'jakarta.persistence.MappedSuperclass'
	annotation 'jakarta.persistence.Embeddable'
}

tasks.named('test') {
	useJUnitPlatform()
}
